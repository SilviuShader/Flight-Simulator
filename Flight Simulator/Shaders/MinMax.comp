#version 430 core
#define BLOCKS_COUNT 8
#define MAX_ITERATIONS 4

layout (local_size_x = BLOCKS_COUNT, local_size_y = BLOCKS_COUNT, local_size_z = 1) in;
layout (r32f, binding = 0) uniform image2D ImageInput1;
layout (r32f, binding = 1) uniform image2D ImageInput2;

layout (r32f, binding = 2) uniform image2D ImageOutput1;
layout (r32f, binding = 3) uniform image2D ImageOutput2;

uniform int Iterations;

shared float s_minValues[BLOCKS_COUNT * BLOCKS_COUNT];
shared float s_maxValues[BLOCKS_COUNT * BLOCKS_COUNT];

void storeMin(uint index, float value)
{
	s_minValues[index] = value;
}

void storeMax(uint index, float value)
{
	s_maxValues[index] = value;
}

float loadMin(uint index)
{
	return s_minValues[index];
}

float loadMax(uint index)
{
	return s_maxValues[index];
}

void main()
{
	ivec2 imgSize = imageSize(ImageInput1);
	ivec2 pixelCoords = ivec2(gl_GlobalInvocationID.xy);

	if (pixelCoords.x >= imgSize.x / 2 || 
	    pixelCoords.y >= imgSize.y / 2)
		return;

	float min1 = imageLoad(ImageInput1, pixelCoords * 2).x;
	float min2 = imageLoad(ImageInput1, pixelCoords * 2 + ivec2(1, 0)).x;
	float min3 = imageLoad(ImageInput1, pixelCoords * 2 + ivec2(0, 1)).x;
	float min4 = imageLoad(ImageInput1, pixelCoords * 2 + ivec2(1, 1)).x;

	float max1 = imageLoad(ImageInput2, pixelCoords * 2).x;
	float max2 = imageLoad(ImageInput2, pixelCoords * 2 + ivec2(1, 0)).x;
	float max3 = imageLoad(ImageInput2, pixelCoords * 2 + ivec2(0, 1)).x;
	float max4 = imageLoad(ImageInput2, pixelCoords * 2 + ivec2(1, 1)).x;

	float mn1 = min(min1, min2);
	float mn2 = min(min3, min4);
	float mnm = min(mn1, mn2);

	float mx1 = max(max1, max2);
	float mx2 = max(max3, max4);
	float mxm = max(mx1, mx2);

	storeMin(gl_LocalInvocationIndex, mnm);
	storeMax(gl_LocalInvocationIndex, mxm);

	if (Iterations == 1)
	{
		imageStore(ImageOutput1, pixelCoords, vec4(mnm, 0.0, 0.0, 1.0));
		imageStore(ImageOutput2, pixelCoords, vec4(mxm, 0.0, 0.0, 1.0));

		return;
	}

	groupMemoryBarrier();
	barrier();

	if ((gl_LocalInvocationIndex & 0x9) == 0)
	{
		min1 = mnm;
		min2 = loadMin(gl_LocalInvocationIndex + 0x01);
		min3 = loadMin(gl_LocalInvocationIndex + 0x08);
		min4 = loadMin(gl_LocalInvocationIndex + 0x09);

		max1 = mxm;
		max2 = loadMax(gl_LocalInvocationIndex + 0x01);
		max3 = loadMax(gl_LocalInvocationIndex + 0x08);
		max4 = loadMax(gl_LocalInvocationIndex + 0x09);

		mnm = min(min(min1, min2), min(min3, min4));
		mxm = max(max(max1, max2), max(max3, max4));

		storeMin(gl_LocalInvocationIndex, mnm);
		storeMax(gl_LocalInvocationIndex, mxm);

		if (Iterations == 2)
		{
			imageStore(ImageOutput1, pixelCoords / 2, vec4(mnm, 0.0, 0.0, 1.0));
			imageStore(ImageOutput2, pixelCoords / 2, vec4(mxm, 0.0, 0.0, 1.0));
			
			return;
		}
	}

	if (Iterations == 2)
		return;

	groupMemoryBarrier();
	barrier();

	if ((gl_LocalInvocationIndex & 0x1B) == 0)
	{
		min1 = mnm;
		min2 = loadMin(gl_LocalInvocationIndex + 0x02);
		min3 = loadMin(gl_LocalInvocationIndex + 0x10);
		min4 = loadMin(gl_LocalInvocationIndex + 0x12);

		max1 = mxm;
		max2 = loadMax(gl_LocalInvocationIndex + 0x02);
		max3 = loadMax(gl_LocalInvocationIndex + 0x10);
		max4 = loadMax(gl_LocalInvocationIndex + 0x12);

		mnm = min(min(min1, min2), min(min3, min4));
		mxm = max(max(max1, max2), max(max3, max4));

		storeMin(gl_LocalInvocationIndex, mnm);
		storeMax(gl_LocalInvocationIndex, mxm);

		if (Iterations == 3)
		{
			imageStore(ImageOutput1, pixelCoords / 4, vec4(mnm, 0.0, 0.0, 1.0));
			imageStore(ImageOutput2, pixelCoords / 4, vec4(mxm, 0.0, 0.0, 1.0));
			
			return;
		}
	}

	if (Iterations == 3)
		return;

	groupMemoryBarrier();
	barrier();

	if (gl_LocalInvocationIndex == 0)
	{
		min1 = mnm;
		min2 = loadMin(gl_LocalInvocationIndex + 0x04);
		min3 = loadMin(gl_LocalInvocationIndex + 0x20);
		min4 = loadMin(gl_LocalInvocationIndex + 0x24);

		max1 = mxm;
		max2 = loadMax(gl_LocalInvocationIndex + 0x04);
		max3 = loadMax(gl_LocalInvocationIndex + 0x20);
		max4 = loadMax(gl_LocalInvocationIndex + 0x24);

		mnm = min(min(min1, min2), min(min3, min4));
		mxm = max(max(max1, max2), max(max3, max4));

		if (Iterations == 4)
		{
			imageStore(ImageOutput1, pixelCoords / 8, vec4(mnm, 0.0, 0.0, 1.0));
			imageStore(ImageOutput2, pixelCoords / 8, vec4(mxm, 0.0, 0.0, 1.0));
			return;
		}
	}
}