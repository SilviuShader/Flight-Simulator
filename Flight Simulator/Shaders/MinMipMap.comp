#version 430 core
#define BLOCKS_COUNT 8
#define MAX_ITERATIONS 4

layout (local_size_x = BLOCKS_COUNT, local_size_y = BLOCKS_COUNT, local_size_z = 1) in;
layout (r32f, binding = 0) uniform image2D ImageInput;
layout (r32f, binding = 1) uniform image2D ImageOutput;

uniform int Iterations;

shared float s_minValues[BLOCKS_COUNT * BLOCKS_COUNT];

void storeMin(uint index, float value)
{
	s_minValues[index] = value;
}

float loadMin(uint index)
{
	return s_minValues[index];
}

void main()
{
	ivec2 imgSize     = imageSize(ImageInput);
	ivec2 pixelCoords = ivec2(gl_GlobalInvocationID.xy);

	if (pixelCoords.x >= imgSize.x / 2 || 
	    pixelCoords.y >= imgSize.y / 2)
		return;

	float min1 = imageLoad(ImageInput, pixelCoords * 2).x;
	float min2 = imageLoad(ImageInput, pixelCoords * 2 + ivec2(1, 0)).x;
	float min3 = imageLoad(ImageInput, pixelCoords * 2 + ivec2(0, 1)).x;
	float min4 = imageLoad(ImageInput, pixelCoords * 2 + ivec2(1, 1)).x;

	float mnm  = min(min(min1, min2), min(min3, min4));

	storeMin(gl_LocalInvocationIndex, mnm);

	if (Iterations == 1)
	{
		imageStore(ImageOutput, pixelCoords, vec4(mnm));
		return;
	}

	groupMemoryBarrier();
	barrier();

	if ((gl_LocalInvocationIndex & 0x9) == 0)
	{
		min1 = mnm;
		min2 = loadMin(gl_LocalInvocationIndex + 0x01);
		min3 = loadMin(gl_LocalInvocationIndex + 0x08);
		min4 = loadMin(gl_LocalInvocationIndex + 0x09);

		mnm = min(min(min1, min2), min(min3, min4));

		storeMin(gl_LocalInvocationIndex, mnm);

		if (Iterations == 2)
		{
			imageStore(ImageOutput, pixelCoords / 2, vec4(mnm));
			return;
		}
	}

	if (Iterations == 2)
		return;

	groupMemoryBarrier();
	barrier();

	if ((gl_LocalInvocationIndex & 0x1B) == 0)
	{
		min1 = mnm;
		min2 = loadMin(gl_LocalInvocationIndex + 0x02);
		min3 = loadMin(gl_LocalInvocationIndex + 0x10);
		min4 = loadMin(gl_LocalInvocationIndex + 0x12);

		mnm = min(min(min1, min2), min(min3, min4));

		storeMin(gl_LocalInvocationIndex, mnm);

		if (Iterations == 3)
		{
			imageStore(ImageOutput, pixelCoords / 4, vec4(mnm));
			return;
		}
	}

	if (Iterations == 3)
		return;

	groupMemoryBarrier();
	barrier();

	if (gl_LocalInvocationIndex == 0)
	{
		min1 = mnm;
		min2 = loadMin(gl_LocalInvocationIndex + 0x04);
		min3 = loadMin(gl_LocalInvocationIndex + 0x20);
		min4 = loadMin(gl_LocalInvocationIndex + 0x24);

		mnm = min(min(min1, min2), min(min3, min4));

		if (Iterations == 4)
		{
			imageStore(ImageOutput, pixelCoords / 8, vec4(mnm));
			return;
		}
	}
}